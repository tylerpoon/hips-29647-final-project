<!DOCTYPE html>
<html>

<head>
    <meta charset="utf-8">
    <title>Shaders</title>
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <script type="text/javascript"
        src="https://rawgit.com/patriciogonzalezvivo/glslCanvas/master/dist/GlslCanvas.js"></script>

    <style>
        html,
        body {
            width: 100%;
            height: 100%;
        }

        h1 {
            text-align: center;
            margin-bottom: 5px;
        }

        p {
            font-size: 20px;
        }

        .content {
            margin: 20px;
        }

        .example {
            margin-bottom: 100px;
            margin-top: 100px;
        }

        .container {
            display: flex;
            flex-direction: "row";
        }

        .explanation {
            margin-top: 0px;
            margin-left: 20px;
        }

        .author {
            margin-top: 5px;
            text-align: center;
        }
    </style>
</head>

<body>
    <div class="content">
        <h1>Shaders</h1>
        <h3 class="author">Tyler Poon</h3>
        <a href="https://github.com/waddlepon/hips-29647-final-project">Source Code</a>
        <h2>Introduction</h2>
        <p>
            For this project I chose to investigate shaders, a type of program used to render graphics, often for
            textural or surface effects.
            I chose to investigate shaders because I had seen incredibly detailed graphics on sites like <a
                href="https://www.shadertoy.com/">Shader Toy</a> that seemed to arise magically from very little
            shader
            code. Since I have no experience in programming shaders, I learned the basics from <a
                href="https://thebookofshaders.com">The Book of Shaders</a> which is an amazing example of teaching
            computer science and math using multimedia. In the process of learning from this book and creating
            shaders,
            I found that while I
            came to appreciate shaders as examples of visualizing and materializing math (like in the "Inside Out"
            reading),
            my experience creating shaders was distinct from how I've previously engaged with math. Mathematical
            functions became tactile
            and responsive in a way that forced me to develop my intuition for the properties and behavior
            of mathematics and motion. Typically when I see a mathematical visualization I have to reverse-engineer
            its
            relationship to mathematics through observation,
            whereas while creating shaders I approached this relationship from the opposite direction, starting with
            a
            mathematical object (e.g. a sine wave, polar coordinates)
            and constructing visuals from the natural projects of that object. This type of exercise was far more
            useful
            for understanding and getting a feel for mathematics than merely seeing a visual,
            and reinforces that in mathematical learning building a relationship with
            mathematical objects through practice is most effective. While the Tripos method had reams of issues,
            the
            emphasis on ridiculous amounts of practice
            described at Cambridge in Warwick's book, while much less fun than writing shaders, developed a similar
            sort
            of relationship between the student and the mathematical objects they worked with. This project gave me
            some
            hope for the existence of alternatives or at least more fun and compelling forms of practice. Although
            mathematics as play is a common idea
            in modern mathematics pedagogy, this project was one of the rare times where mathematics actually became
            play for me, and gives me hope that mathematics can be made an object of play instead of fear.
        </p>
        <h2>What are shaders?</h2>
        <p>
            Shaders are computer graphics programs, that as described earlier are often used for textures in 3D
            graphics
            (e.g. rendering a floor tile with a glossy texture),
            although they are incredibly versatile, and as a result are common in the <a
                href="https://en.wikipedia.org/wiki/Demoscene">demoscene</a> for creating computer-rendered art.
            The most common shader language is GLSL (OpenGL Shading Language) which is a C/C++ style language.
            These shaders can be ran on many different platforms, but I'm using WebGL (Web Graphics Library) to run
            these shaders in the browser.
            The type of shaders I made for this project are fragment shaders, which work on a per-pixel level, i.e.
            they
            give instructions to each individual pixel. Fragment shaders can be thought of as a single program that
            is
            ran by each pixel individually in parallel,
            where the behavior of each pixel only differs based on its location on the screen. The pixels don't know
            anything
            about the other pixels on the screen, they just execute the shader program in isolation, although they
            all
            can see some universal values
            like the current time and the location of the mouse. An example of how this type of logic works in
            practice,
            is if I wanted to draw a red diagonal line with slope 1 across the screen, I write my shader program "if
            your x
            and y coordinates are the same, turn red"
            so when each pixel on this line runs the shader, it will turn red, rendering our line on the screen.
            This model of programming might seem weird at first, but it exploits the parallelism of modern GPUs,
            which have many tiny cores that can execute the shader program for all the pixels in parallel, which is
            much
            faster than computing the program for each pixel linearly, one at a time.
            GPUs also have special optimizations for certain mathematical functions (e.g. the trigonometric
            functions)
            which
            encourages the programmer to use a small toolbox of mathematical functions in clever ways. The specific
            restrictions
            on shader programming that allow for efficient rendering make it a creative mathematical endeavor.
        </p>
        <h3>Note: Refresh the browser to reset the time if the shaders get messed up</h2>
            <div class="example">
                <h2> Variations on a Proof Fractal </h2>
                <div class="container">
                    <canvas class="glslCanvas" data-fragment="
#ifdef GL_ES
precision mediump float;
#endif

uniform vec2 u_resolution;
uniform float u_time;

#define ITERS 50
#define MIN_A -50.
#define LENGTH_A 100.
#define MIN_B -50.
#define LENGTH_B 100. 

#define PI 3.14159265359

/// COSH Function (Hyperbolic Cosine)
float cosh(float val)
{
    float tmp = exp(val);
    float cosH = (tmp + 1.0 / tmp) / 2.0;
    return cosH;
}
 
// TANH Function (Hyperbolic Tangent)
float tanh(float val)
{
    float tmp = exp(val);
    float tanH = (tmp - 1.0 / tmp) / (tmp + 1.0 / tmp);
    return tanH;
}
 
// SINH Function (Hyperbolic Sine)
float sinh(float val)
{
    float tmp = exp(val);
    float sinH = (tmp - 1.0 / tmp) / 2.0;
    return sinH;
}

#define cx_mul(a, b) vec2(a.x*b.x - a.y*b.y, a.x*b.y + a.y*b.x)
#define cx_div(a, b) vec2(((a.x*b.x + a.y*b.y)/(b.x*b.x + b.y*b.y)),((a.y*b.x - a.x*b.y)/(b.x*b.x + b.y*b.y)))
#define cx_sin(a) vec2(sin(a.x) * cosh(a.y), cos(a.x) * sinh(a.y))
#define cx_cos(a) vec2(cos(a.x) * cosh(a.y), -sin(a.x) * sinh(a.y))

vec2 as_polar(vec2 z) {
  return vec2(
    length(z),
    atan(z.y, z.x)
  );
}

vec2 cx_tan(vec2 a) {return cx_div(cx_sin(a), cx_cos(a)); }
vec2 cx_log(vec2 a) {
    vec2 polar = as_polar(a);
    float rpart = polar.x;
    float ipart = polar.y;
    if (ipart > PI) ipart=ipart-(2.0*PI);
    return vec2(log(rpart),ipart);
}
vec2 cx_pow(vec2 v, float p) {
  vec2 z = as_polar(v);
  return pow(z.x, p) * vec2(cos(z.y * p), sin(z.y * p));
}

vec2 my_polynomial(vec2 x) {
    return cx_pow(x, 3.) - 6. * cx_pow(x, 2.) + 9. * x - vec2(4., 0.);
}

vec2 my_deriv(vec2 x) {
    return 3. * cx_pow(x, 2.) - 12. * x + vec2(9., 0.);
}

int newton_method(vec2 x0) {
    vec2 current = x0;
    for (int i = 0; i < ITERS; i++) {
        if (distance(current, vec2(1., 0)) < 0.01 || distance(current, vec2(4., 0)) < 0.01) {
            return i;
        }
        current = current - cx_div(my_polynomial(current), my_deriv(current));
    }
    return ITERS;
}

float modI(float a,float b) {
    float m=a-floor((a+0.5)/b)*b;
    return floor(m+0.5);
}

void main() {
    vec2 st = gl_FragCoord.xy/u_resolution;

    float length_a = LENGTH_A / exp(u_time / 5.);
    float min_a = MIN_A + (LENGTH_A - length_a) / 1.886797;
    float length_b = LENGTH_B / exp(u_time / 5.);
    float min_b = MIN_B + (LENGTH_B - length_b) / 2.;

    vec2 x0 = vec2((length_a * st.x) + min_a, (length_b * st.y) + min_b);
    vec3 color = vec3(modI(float(newton_method(x0)), 2.));

    gl_FragColor = vec4(color, 1.0);
}
" width="500" height="500"></canvas>
                    <p class="explanation">
                        This shader is a recreation of the fractal from the psychedelic proof in 99 Variations on a
                        Proof.
                        Like in the proof in 99 Variations, each pixel represents a point on the complex plane, and
                        is
                        colored black
                        if Newton's method for finding roots converges (within some error threshold) in even
                        iterations
                        and white otherwise.
                        Unlike the picture in 99 Variations, my shader zooms in as time goes by, which emphasizes
                        the
                        fractal-like nature of
                        this "proof". I wouldn't consider the initial picture a proof, and while I don't think the
                        addition of motion makes it any more of a proof,
                        it does make it more compelling visually and mathematically.
                        You may notice that my version looks quite different from the version in the book and that
                        the
                        image breaks down the further (e.g. the edges get more jagged and shaky) it zooms in.
                        This is because as floating point numbers (how computers store decimals) get more precise,
                        the computer's operations with them get increasingly inaccurate. Computer graphics deals in
                        approximations,
                        and almost any visual we come up with will necessitate some error, which makes the question
                        of computer visual as proof more fraught.
                    </p>
                </div>
            </div>
            <div class="example">
                <h2>Variation on a Proof Plot</h2>
                <div class="container">
                    <canvas class="glslCanvas" data-fragment="
#ifdef GL_ES
precision mediump float;
#endif

#define PI 3.14159265359

uniform vec2 u_resolution;
uniform float u_time;

float plot(vec2 st, float pct){
  return  smoothstep( pct-0.01, pct, st.y) -
          smoothstep( pct, pct+0.01, st.y);
}

//This one needs a lot of work but was good practice
void main() {
    vec2 st = gl_FragCoord.xy/u_resolution;

    float x = (u_time / 10.) + st.x * 3.;
    float y = pow(x, 3.) - 6. * pow(x, 2.) + 11. * x - 6.;
    y = (y + 2. - u_time / 5.) / 3.;

    float pct = plot(st,y);
    float pct_lin = plot(st, ((2. * x - 2.) + 2. - u_time / 5.) / 3.);
    vec3 color = pct*vec3(0.0,1.0,0.0) + pct_lin*vec3(1.0, 0.0, 0.0);

    gl_FragColor = vec4(color,1.0);
}" width="500" height="500"></canvas>
                    <p class="explanation">
                        This is another proof of the problem from 99 Variations on a Proof.
                        The red line graphs the linear function on the right-hand side of the equation, while the
                        green line graphs the cubic on the left-hand side. Overtime, the shader "moves" North-East
                        on the graph by shifting the (x,y) coordinate each pixel corresponds to. For example, the
                        bottom-left
                        pixel starts out as representing (0,0) on the graph, but at later times will represent
                        higher coordinates on the graph.
                        Is there a difference between seeing a larger frame of the graph versus seeing a moving frame
                        along a graph
                        in a visual's verasity of a proof? Personally, a static graph intuitively feels more "true" than
                        one with movement; movement seems to allow for some amount of trickery or illusion.
                        Similarly to the previous shader, you may notice some inconsistencies with the thickness of the
                        green line
                        which is again caused by differences in floating point calculation accuracy at different points
                        on the graph.
                        Even for relatively simple objects, the computational limitations for shaders are immediately
                        apparent.
                    </p>
                </div>
            </div>

            <div class="example">
                <h2>Sine Sunset</h2>
                <div class="container">
                    <canvas class="glslCanvas" data-fragment="
#ifdef GL_ES
precision mediump float;
#endif

#define PI 3.14159265359

uniform vec2 u_resolution;
uniform float u_time;

vec3 color_s = vec3(0.5,0.8,0.9);

float plot (vec2 st, float pct){
  return  smoothstep( pct-0.05, pct, st.y) -
          smoothstep( pct, pct+0.05, st.y);
}

void main() {
    vec2 st = gl_FragCoord.xy/u_resolution;

    vec2 sun_coords = vec2(mod(u_time, PI) / PI, abs(sin(u_time)));

    float d = distance(sun_coords, st) * 2.;
    float pct = 1.0 - smoothstep(0., 0.3, d);

    vec3 color = vec3(0.0);
    color = mix(color, color_s, abs(sin(u_time))); 
    color = mix(color, vec3(0.99,0.72,0.07), pct);
    gl_FragColor = vec4(color, 1.0);
}" width="500" height="500"></canvas>
                    <p class="explanation">
                        This shader uses the absolute value of a sine function on the time to create a smooth, periodic
                        motion that determines the location of the animated sun and the
                        color on the screen. Every pixel on the screen "mixes" a percentage of light-blue into its color
                        based on the absolute value of the sine function on time. Then, each pixel calculates the
                        coordinates of the sun given the
                        absolute value of a sine function on the time, and based on its distance to that coordinante,
                        "mixes" a percentage of orange into its color.
                        This method creates a circle around the center (the coordinate for the sun) with a slight
                        glowing effect.
                        While it's a simple example, this shader demonstrates the versatility of mathematical functions
                        in computer graphics: the sine wave is used for both motion
                        and color, and with tweaking, many different types of motion could be created using this sine
                        wave.
                    </p>
                </div>
            </div>

            <div class="example">
                <h2>Circles</h2>
                <div class="container">
                    <canvas class="glslCanvas" data-fragment="
#ifdef GL_ES
precision mediump float;
#endif

uniform vec2 u_resolution;
uniform vec2 u_mouse;
uniform float u_time;

void main(){
	vec2 st = gl_FragCoord.xy/u_resolution;
    float pct = 0.0;

    pct = min(distance(st,vec2(0.4)),distance(st,vec2(0.6)));

    float cutoff = abs(sin(u_time)) * 0.8 + 0.1;
	pct = 1. - step(cutoff, pct * 2.);
    vec3 color = vec3(0.9, 0.3, 0.4) * pct;

	gl_FragColor = vec4( color, 1.0 );
}" width="500" height="500"></canvas>
                    <p class="explanation">
                        In the previous shader, the circle was created by mixing an amount of color
                        based on the distance from the center of the sun. In this shader, we use a strict cutoff
                        to determine if we should mix any amount of pink, creating uniformly shaded circles. This cutoff
                        is determined by the absolute value of a sine wave of time, showing that size is yet another
                        form of motion
                        which can be constructed with a simple sine wave. To have two circles appear instead of one,
                        each pixel
                        calculates it distance from the center of each circle and then uses the minimum distance of
                        those two distances
                        to determine if it makes the cutoff to be colored. While this is a very simple example,
                        combining distance fields (like we did here with the minimum of the distance from each circle)
                        is an important shader technique that can create all sorts of complex graphics.
                    </p>
                </div>
            </div>

            <div class="example">
                <h2>Polygons</h2>
                <div class="container">
                    <canvas class="glslCanvas" data-fragment="
#ifdef GL_ES
precision mediump float;
#endif

uniform vec2 u_resolution;
uniform float u_time;

#define PI 3.14159265359
#define TWO_PI 2.*PI

float polygon(vec2 st, vec2 offset, int sides) {
    st = st *2.-1. + offset;

    float a = atan(st.x,st.y)+PI;
    float r = TWO_PI/float(sides);
    return cos(floor(.5+a/r)*r-a)*length(st);
}

void main() {
    vec2 st = gl_FragCoord.xy/u_resolution;

    int sides = int(mod(u_time, 10.));

    float oddshape = 1.0 - smoothstep(0.4, 0.41, polygon(st, vec2(-0.4), sides * 2 + 3));
    float evenshape = 1.0 - smoothstep(0.4, 0.41, polygon(st, vec2(0.4), sides * 2 + 4));
    vec3 color = vec3(max(oddshape, evenshape));
    gl_FragColor = vec4(color, 1.0);
}" width="500" height="500"></canvas>
                    <p class="explanation">
                        This shader renders two polygons of increasing sides overtime. It is not immediately obvious (or
                        at least it wasn't to me)
                        how to create a polygon of arbitrary side length using a shader. This method works first
                        translating our coordinates
                        of (0,0)-(1,1) to coordinates that go from (-1,-1)-(1,1). This places the origin at the center
                        of the screen. We
                        can then calculate the angle and distance from the center of our pixel (i.e. polar coordinates)
                        and use
                        the angle to calculate which side of the polygon our angle corresponds to.
                        We then use trigionometry to determine the distance of the straight line from the origin to the
                        center of the side of the polygon
                        that our point would lie on (notice this distance is the same for each point on the same side).
                        This way, we can use a cutoff to only render the polygon for sides up to some distance from the
                        origin in a way that preserves the shape of the polygon,
                        as if we were adding extra "thickness" to the polygon instead of adding individual points.
                        An added offset is used to move the origin of the polygon to different locations on the screen.
                        This is not an intuitive way to think about drawing a polygon or geometry in general, and
                        demonstrates
                        another way in which creating computer graphics forces a different understanding of commmon
                        mathematical objects
                        because of its particular model of rendering.
                    </p>
                </div>
            </div>

            <div class="example">
                <h2>Color Wheel</h2>
                <div class="container">
                    <canvas class="glslCanvas" data-fragment="
#ifdef GL_ES
precision mediump float;
#endif

#define TWO_PI 6.28318530718

uniform vec2 u_resolution;
uniform float u_time;

//  Function from IÃ±igo Quiles
//  https://www.shadertoy.com/view/MsS3Wc
vec3 hsb2rgb( in vec3 c ){
    vec3 rgb = clamp(abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),
                             6.0)-3.0)-1.0,
                     0.0,
                     1.0 );
    rgb = rgb*rgb*(3.0-2.0*rgb);
    return c.z * mix( vec3(1.0), rgb, c.y);
}

void main(){
    vec2 st = gl_FragCoord.xy/u_resolution;
    vec3 color = vec3(0.0);

    // Use polar coordinates instead of cartesian
    vec2 toCenter = vec2(0.5)-st;
    float angle = atan(toCenter.y,toCenter.x);
    float radius = length(toCenter)*2.0;
    float time_mod = mod(u_time, 5.) / 5.;

    // Map the angle (-PI to PI) to the Hue (from 0 to 1)
    // and the Saturation to the radius
    color = hsb2rgb(vec3((angle/TWO_PI)+time_mod,radius,1.0));

    gl_FragColor = vec4(color,1.0);
}" width="500" height="500"></canvas>
                    <p class="explanation">
                        There are a few common ways to conceive of colors in mathematics. By default,
                        GLSL uses RGB, which uses three values corresponding to the amount of red, green, and blue is in
                        the color.
                        The color wheel in this shader uses another color space called HSB, with three numbers that
                        correspond to the hue, saturation, and brightness of a color.
                        In this shader, the angle of each pixel determines its hue and the radius is determined by its
                        saturation. All the colors in the shader have max brightness.
                        I use the modulus of time to rotate the color wheel by changing the corresponding angle of each
                        pixel. The modulus is another function
                        that can be used to create periodic motion, although it creates a much more linear motion that a
                        sine wave.
                        Different color spaces make certain effects easier or harder to create, since "moving through" a
                        color space by a function of its coordinates
                        will create completely different colors. Thinking of color within these different mathematical
                        color spaces
                        or warping and shaping these color spaces to create an effect requires a combination of
                        mathematical and visual thinking
                        that bolsters understanding of both math and visual space, which is an example of why I find
                        shaders such an intersting
                        pedagogical exercise.
                    </p>
                </div>
            </div>

            <div class="example">
                <h2>Rotating Tiles</h2>
                <div class="container">
                    <canvas class="glslCanvas" data-fragment="
#ifdef GL_ES
precision mediump float;
#endif

uniform vec2 u_resolution;
uniform float u_time;

#define PI 3.14159265359

vec2 rotate2D (vec2 _st, float _angle) {
    _st -= 0.5;
    _st =  mat2(cos(_angle),-sin(_angle),
                sin(_angle),cos(_angle)) * _st;
    _st += 0.5;
    return _st;
}

vec2 rotateTilePattern(vec2 _st){

    //  Scale the coordinate system by 2x2
    _st *= 2.0;

    //  Give each cell an index number
    //  according to its position
    float index = 0.0;
    index += step(1., mod(_st.x,2.0));
    index += step(1., mod(_st.y,2.0))*2.0;

    //      |
    //  2   |   3
    //      |
    //--------------
    //      |
    //  0   |   1
    //      |

    // Make each cell between 0.0 - 1.0
    _st = fract(_st);

    // Rotate each cell according to the index
    if(index == 1.0){
        //  Rotate cell 1 by 90 degrees
        _st = rotate2D(_st,PI*0.5);
    } else if(index == 2.0){
        //  Rotate cell 2 by -90 degrees
        _st = rotate2D(_st,PI*-0.5);
    } else if(index == 3.0){
        //  Rotate cell 3 by 180 degrees
        _st = rotate2D(_st,PI);
    }

    return _st;
}

void main() {
    vec2 st = gl_FragCoord.xy/u_resolution;
    vec3 color = vec3(0.0);

    st *= 3.0;
    st = fract(st);
    st = rotateTilePattern(st);
    st = rotate2D(st,PI*u_time*0.25);

    color = vec3(step(pow(st.x, st.y), 0.8));

    gl_FragColor = vec4(color, 1.0);
}" width="500" height="500"></canvas>
                    <p class="explanation">
                        Tiles can be made in shaders by scaling up the coordinate system, and then using modulo or
                        fractional operations
                        to divide the screen into multiple smaller coordinate systems that act as tiles. This particular
                        shader splits the screen first into
                        6 tiles, and then each tile is split into 4 more coordinate systems, each rendering the same
                        object but rotated
                        at a different orientation. The rotation is performed by multiplying a rotation matrix with the
                        coordinates.
                        Another reason for GPUs efficiency at rendering graphics is its ability to parallelize matrix
                        operations like the rotation matrix multiplication, which
                        are common in most computer graphics workloads, especially 3D graphics.
                    </p>
                </div>
            </div>
            <div class="example">
                <h2>Noisy Sky</h2>
                <div class="container">
                    <canvas class="glslCanvas" data-fragment="
#ifdef GL_ES
precision mediump float;
#endif

uniform vec2 u_resolution;
uniform float u_time;

#define PI 3.14159265359

vec3 color_1 = vec3(0.6549, 0.8902, 0.9686);
vec3 color_2 = vec3(0.1137, 0.0275, 0.7686);

// 2D Random
float random (in vec2 st) {
    return fract(sin(dot(st.xy,
                         vec2(12.9898,78.233)))
                 * 43758.5453123);
}

// 2D Noise based on Morgan McGuire @morgan3d
// https://www.shadertoy.com/view/4dS3Wd
float noise (in vec2 st) {
    vec2 i = floor(st);
    vec2 f = fract(st);

    // Four corners in 2D of a tile
    float a = random(i);
    float b = random(i + vec2(1.0, 0.0));
    float c = random(i + vec2(0.0, 1.0));
    float d = random(i + vec2(1.0, 1.0));

    // Smooth Interpolation

    // Cubic Hermine Curve.  Same as SmoothStep()
    vec2 u = f*f*(3.0-2.0*f);
    // u = smoothstep(0.,1.,f);

    // Mix 4 coorners percentages
    return mix(a, b, u.x) +
            (c - a)* u.y * (1.0 - u.x) +
            (d - b) * u.x * u.y;
}

float monster_noise(in vec2 st) {
    vec2 pos = st + vec2(u_time) * 0.2;
    float val = 0.;

    val += noise(pos);
    val += noise(pos * 2.) / 2.;
    val += noise(pos * 4.) / 4.;
    val += noise(pos * 8.) / 8.;
    val /= 1. + 1./2. + 1./4. + 1./8.;

    val = noise(pos + val);

    return val;
}

void main() {
    vec2 st = gl_FragCoord.xy/u_resolution;
    vec3 color = vec3(0.0);
    vec2 pos = 3.0 * st;

    float val = monster_noise(pos) * 0.5 + monster_noise(pos / 2.) * 0.5;
    color = vec3(mix(color_1, color_2, val));

    gl_FragColor = vec4(color, 1.0);
}" width="500" height="500"></canvas>
                    <p class="explanation">
                        Randomness in computers is hard to come by. Computers use pseudorandom number generation to
                        produce randomness, a mathematical process
                        which takes some seed (i.e. a number) that is used to generate a noisy, "random" output. To
                        produce randomness in GLSL,
                        we can use a sine function with a tiny period (the sine waves get smooshed together) and take
                        the fractional result (the numbers after
                        the decimal) to
                        get a "random"-looking output. Because this process is pseudorandom (i.e. is actually
                        deterministic), notice that if you reset the time (by restarting the browser)
                        the generated sky will look exactly the same.
                        To create this shader, we use Perlin noise with our randomness function, which interpolates
                        across the plane with random
                        values to give a smooth, but "random"-looking 2D map (look it up on google to see what these
                        maps look like).
                        By playing with this noise algorithm, I was able to generate a somewhat realistic, but
                        "random"-looking
                        version of a sky.
                        This technique is super flexible, and noise-based generation is common in computer graphics for
                        replicating patterns in nature,
                        like waves or clouds. While there is some complex math involved in generating good-looking
                        noise, there is certainly
                        an art to creating a mix of structure and unpredictability that feels realistic and looks
                        interesting, and an amount of feel and intuition necessary to transform noise into a
                        compelling graphic.
                        Even though the visual is generated "randomly", the process of making this shader felt the most
                        creative out of all of them, as I felt like I was trying to tame mathematics instead of merely
                        command it.
                    </p>
                </div>
            </div>
</body>

</html>